<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark" 
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:selector="com.fTheme.view.selector.*"
	xmlns:components="com.fTheme.components.*"
	preinitialize="preinit();">
	
	<fx:Metadata>
		[SWF(width="1000", height="800")]
	</fx:Metadata>
	
	<fx:Script>
	<![CDATA[
		import com.fTheme.FThemeController;
		import com.fTheme.look.Look;
		import com.fTheme.look.LookLink;
		import com.fTheme.look.LookManager;
		
		import flash.net.navigateToURL;
		
		[Bindable]
		private var lookManager:LookManager;
		
		//--------------------------------------
		//  look
		//--------------------------------------

		private var _lookLink:LookLink;

		public function get lookLink():LookLink
		{
			return _lookLink;
		}

		public function set lookLink(value:LookLink):void
		{
			if (_lookLink == value)
				return;
			
			_lookLink = value;
			setJSLook(_lookLink ? _lookLink.name : null);
		}
		
		//--------------------------------------------------------------------------
		//
		//  Methods
		//
		//--------------------------------------------------------------------------

		private function preinit():void
		{
			lookManager = (new FThemeController()).lookManager;
			lookManager.addEventListener(Event.INIT, lookManager_initHandler);
		}
		
		private function getJSLook():String
		{
			if (!ExternalInterface.available)
				return null;

			try
			{
				return ExternalInterface.call("getLook");
			}
			catch (error:*) {}
			return null;
		}
		
		private function setJSLook(name:String):void
		{
			if (!ExternalInterface.available)
				return;
			
			try
			{
				ExternalInterface.call("setLook", name);
			}
			catch (error:*) {}
		}
		
		//--------------------------------------------------------------------------
		//
		//  Event handlers
		//
		//--------------------------------------------------------------------------

		private function lookManager_initHandler(event:Event):void
		{
			var jsLook:String = getJSLook();
			if (jsLook)
				lookManager.lookLink = lookManager.getLookLink(jsLook);
			
			_lookLink = lookManager.lookLink;
			lookManager.addEventListener("lookLinkChange", lookManager_lookLinkChangeHandler);
		}
		
		private function lookManager_lookLinkChangeHandler(event:Event):void
		{
			lookLink = lookManager.lookLink;
		}

	]]>
	</fx:Script>

	<s:layout>
		<s:HorizontalLayout paddingBottom="10" paddingLeft="10" paddingRight="10"
			paddingTop="10" gap="10"/>
	</s:layout>
	
	<components:LookSelector direction="vertical" maxHeight="400"
		visible="{lookManager.lookLinks.length > 1}"
		includeInLayout="{lookManager.lookLinks.length > 1}"/>
	
	<components:Sample/>
	
</s:Application>
